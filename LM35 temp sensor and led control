#include <ESP8266WiFi.h>
#include <PubSubClient.h>

const char* ssid = "Ghost_RTR";          // Your WiFi SSID
const char* password = "appuvedha";  // Your WiFi password

const char* mqtt_server = "test.mosquitto.org";  // MQTT Broker

WiFiClient espClient;
PubSubClient client(espClient);
unsigned long lastMsg = 0;
#define MSG_BUFFER_SIZE 50
char msg[MSG_BUFFER_SIZE];

void setup_wifi() {
  delay(10);
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  randomSeed(micros());
  Serial.println("\nWiFi connected");
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());
}

void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  for (int i = 0; i < length; i++) {
    Serial.print((char)payload[i]);
  }
  Serial.println();

  if ((char)payload[0] == '1') {
    digitalWrite(BUILTIN_LED, LOW);  // Turn ON
    Serial.println("Built-in LED ON");
  } else if ((char)payload[0] == '0') {
    digitalWrite(BUILTIN_LED, HIGH); // Turn OFF
    Serial.println("Built-in LED OFF");
  }
}

void reconnect() {
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    String clientId = "ESP8266Client-";
    clientId += String(random(0xffff), HEX);

    if (client.connect(clientId.c_str())) {
      Serial.println("connected");
      client.subscribe("MQTT");
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      delay(5000);
    }
  }
}

void setup() {
  pinMode(BUILTIN_LED, OUTPUT);
  digitalWrite(BUILTIN_LED, HIGH); // OFF by default

  Serial.begin(115200);
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
}

void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  unsigned long now = millis();
  if (now - lastMsg > 2000) {
    lastMsg = now;

    int analogValue = analogRead(A0); // Read analog from LM35
    float voltage = analogValue * (1.0 / 1023.0); // convert to voltage (0 - 1V)
    float temperatureC = voltage * 100.0; // LM35 gives 10mV/Â°C

    snprintf(msg, MSG_BUFFER_SIZE, "temp=%.2f C", temperatureC);
    Serial.print("Publish message: ");
    Serial.println(msg);
    client.publish("MQTT", msg);
  }
}
